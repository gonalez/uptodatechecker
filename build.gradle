/*
 * Copyright 2022 - Gaston Gonzalez (Gonalez)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.gonalez.uptodatechecker'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation 'com.google.guava:guava:24.0-jre'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'UpToDateChecker'
                url = 'https://github.com/gonalez/uptodatechecker'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/gonalez/uptodatechecker.git'
                    developerConnection = 'scm:git:git@github.com:gonalez/uptodatechecker.git'
                    url = 'https://github.com/gonalez/uptodatechecker'
                }

                developers {
                    developer {
                        id = 'gonalez'
                        name = 'Gaston Gonzalez'
                        email = 'gastongaga202@gmail.com'
                    }
                }
            }

            repositories {
                maven {
                    def releaseUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
                    credentials {
                        if (rootProject.hasProperty('ossrhUsername')
                                && rootProject.hasProperty('ossrhPassword')) {
                            username = rootProject.ossrhUsername
                            password = rootProject.ossrhPassword
                        }
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}